pragma Singleton

import Quickshell
import Quickshell.Io
import QtQuick
import Quickshell.Networking
import Quickshell.Bluetooth

Singleton {
  id: root

  property string cpuUsage: "0%"
  property string memoryUsage: "0%"
  property int batteryLevelRaw: 0
  property string batteryLevel: "0%"
  property string batteryIcon: "󰂎"
  property bool batteryCharging: false
  property string temperature: "0°C"

  // CPU Usage
  Process {
    id: cpuProc
    command: ["sh", "-c", "top -bn1 | grep 'Cpu(s)' | sed 's/.*, *\\([0-9.]*\\)%* id.*/\\1/' | awk '{print 100 - $1\"%\"}'"]
    running: false

    stdout: StdioCollector {
      onStreamFinished: {
        root.cpuUsage = text.trim()
      }
    }
  }

  // Memory Usage
  Process {
    id: memProc
    command: ["sh", "-c", "free | grep Mem | awk '{printf \"%.1f%%\", ($3/$2) * 100.0}'"]
    running: false

    stdout: StdioCollector {
      onStreamFinished: {
        root.memoryUsage = text.trim()
      }
    }
  }

  // Network Info (ethernet takes priority over wifi)
  readonly property var networkDevices: Networking.devices ? Networking.devices.values : []
  readonly property bool isEthernetConnected: {
    return networkDevices.some(device => 
      device.type === DeviceType.Wired && device.connected
    )
  }
  readonly property var connectedWifiNetwork: {
    const wifiDevice = networkDevices.find(device => device.type === DeviceType.Wifi && device.connected);
    if (!wifiDevice || !wifiDevice.networks) return null;
    return wifiDevice.networks.values.find(net => net.connected);
  }
  property string networkType: {
    if (isEthernetConnected) return "ethernet";
    else if (connectedWifiNetwork) return "wifi";
    else return "disconnected";
  }
  property string networkInfo: {
    if (isEthernetConnected) return "Ethernet";
    if (connectedWifiNetwork) return connectedWifiNetwork.name;
    return "Disconnected";
  }



  // Bluetooth Info
  property var bluetoothState: toString(Bluetooth.state)
  readonly property var bluetoothDevices: Bluetooth.devices ? Bluetooth.devices.values : []

  readonly property bool isBluetoothConnected: bluetoothDevices.length > 0

  readonly property var primaryBluetoothDevice: {
      // Finds the first connected device if any exist
      return isBluetoothConnected ? bluetoothDevices[0] : null
  }

  property string bluetoothStatus: {
      if (!Bluetooth.defaultAdapter || !Bluetooth.defaultAdapter.enabled) return "disabled";
      if (isBluetoothConnected) return "connected";
      return "disconnected";
  }

  property string bluetoothInfo: {
      if (!Bluetooth.defaultAdapter || !Bluetooth.defaultAdapter.enabled) return "Disabled";
      if (isBluetoothConnected && primaryBluetoothDevice) {
          // You can use .name (user alias) or .deviceName (hardware name)
          return primaryBluetoothDevice.name; 
      }
      return "Disconnected";
  }




  // Process {
  //   id: netProc
  //   command: ["sh", "-c", "eth=$(nmcli -t -f type,state dev 2>/dev/null | grep '^ethernet:connected'); if [ -n \"$eth\" ]; then echo 'ethernet:Ethernet'; else wifi=$(nmcli -t -f active,ssid dev wifi 2>/dev/null | grep '^yes' | cut -d: -f2); if [ -n \"$wifi\" ]; then echo \"wifi:$wifi\"; else echo 'disconnected:'; fi; fi"]
  //   running: false

  //   stdout: StdioCollector {
  //     onStreamFinished: {
  //       const result = text.trim()
  //       const colonIdx = result.indexOf(':')
  //       const type = result.substring(0, colonIdx)
  //       const info = result.substring(colonIdx + 1)
  //       root.networkType = type
  //       root.networkInfo = info || "Disconnected"
  //     }
  //   }
  // }

  // Battery
  Process {
    id: batteryProc
    command: ["sh", "-c", "printf '%s\\n%s' \"$(cat /sys/class/power_supply/BAT*/capacity 2>/dev/null || echo '99')\" \"$(cat /sys/class/power_supply/BAT*/status 2>/dev/null || echo 'Discharging')\""]
    running: false

    stdout: StdioCollector {
      onStreamFinished: {
        const lines = text.trim().split("\n")
        const level = parseInt(lines[0]) || 0
        const status = (lines[1] || "Discharging").trim()

        root.batteryLevelRaw = level
        root.batteryLevel = level + "%"
        root.batteryCharging = status === "Charging"

        if (root.batteryCharging) root.batteryIcon = ""
        else if (level >= 90) root.batteryIcon = "󰁹"
        else if (level >= 80) root.batteryIcon = "󰂂"
        else if (level >= 70) root.batteryIcon = "󰂁"
        else if (level >= 60) root.batteryIcon = "󰂀"
        else if (level >= 50) root.batteryIcon = "󰁿"
        else if (level >= 40) root.batteryIcon = "󰁾"
        else if (level >= 30) root.batteryIcon = "󰁽"
        else if (level >= 20) root.batteryIcon = "󰁼"
        else if (level >= 10) root.batteryIcon = "󰁻"
        else root.batteryIcon = "󰁺"
      }
    }
  }

  // Temperature
  Process {
    id: tempProc
    command: ["sh", "-c", "sensors 2>/dev/null | grep -E 'Package id 0|Tctl' | head -1 | awk '{print $2}' | sed 's/+//' || echo 'N/A'"]
    running: false

    stdout: StdioCollector {
      onStreamFinished: {
        root.temperature = text.trim() || "N/A"
      }
    }
  }

  // Update timer
  // Timer {
  //   interval: 2000
  //   running: true
  //   repeat: true
  //   onTriggered: {
  //     // cpuProc.running = true
  //     // memProc.running = true
  //     netProc.running = true
  //     // batteryProc.running = true
  //     // tempProc.running = true
  //   }
  // }
}
